#!/usr/bin/env perl

use warnings;
# vi: set ts=4 sw=4 :

use lib "cgi-bin";
use MusicBrainz;
use UserStuff;
use Sql;

my $mb = new MusicBrainz;
$mb->Login;
my $sql = new Sql($mb->{dbh});

my $accs = $sql->SelectListOfHashes("SELECT * FROM moderator where email!='' AND emailconfirmdate IS NULL ORDER BY name");
# my $accs = $sql->SelectListOfHashes("SELECT * FROM moderator where name='djce'");

my $users = UserStuff->new($mb->{dbh});

for my $acc (@$accs)
{
    my $email = delete $acc->{email};
    my $user = $users->_new_from_row($acc);
    my $error = $user->SendFirstVerificationEmail($email);
    warn "Failed: ".$acc->{name}." ($error)\n" if $error;
    print "Sent to $acc->{name} ($email)\n";
}

sub UserStuff::SendFirstVerificationEmail
{
    my ($self, $email) = @_;

    my $name = $self->GetName;
    my $url = $self->GetEmailActivationLink($email);

    use Text::Wrap qw( fill $columns );
    local $columns = 70;
    my $body = fill("", "", <<EOF) . "\n"
Hello,

This email is about your account at MusicBrainz, "$name".
According to your account details your email address is
$email,
so we're just sending you this message so that we can confirm
that this address is correct.
EOF
    . <<EOF;

Please click on the link below to verify your email address:

$url

If clicking on the link does not work, you may need to cut and paste
the link into your web browser manually.

If you do not verify your address within 30 days, we will remove the
email address from your account details.  Your account will still be
active though, so you can continue to use MusicBrainz.

Thanks for using MusicBrainz!

-- The MusicBrainz Team
EOF

    require MusicBrainz::Server::Mail;
    my $mail = MusicBrainz::Server::Mail->new(
        Sender          => 'Webserver <webserver@musicbrainz.org>',
        From            => 'MusicBrainz <noreply@musicbrainz.org>',
        To                      => MusicBrainz::Server::Mail->format_address_line($self->GetName, $email),
        "Reply-To"      => 'MusicBrainz Support <support@musicbrainz.org>',
        Subject         => "email address verification",
        Type            => "text/plain",
        Encoding        => "quoted-printable",
        Data            => $body,
    );
    $mail->attr("content-type.charset" => "utf-8");

    $self->SendFormattedEmail(entity => $mail, to => $email);
}

# eof
