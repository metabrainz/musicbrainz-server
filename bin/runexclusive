#!/usr/bin/env perl

use warnings;
use strict;

my $lockfile;
my $wait = 1;
my $timeout;

use English;
use Getopt::Long;
GetOptions(
    'lockfile|f=s'              => \$lockfile,
    'wait|w'                    => sub { $wait = 1 },
    'nowait|no-wait|n'  => sub { $wait = 0 },
    'timeout|t=i'               => \$timeout,
) or exit 2;

defined($lockfile) or die "No lockfile defined\n";

use Fcntl qw( LOCK_EX LOCK_NB );
open(my $fh, '>', $lockfile) or die $OS_ERROR;

$SIG{ALRM} = sub {};
alarm($timeout) if $timeout;

unless (flock($fh, LOCK_EX | ($wait ? 0 : LOCK_NB)))
{
    exit 100 if $OS_ERROR{EWOULDBLOCK};
    die $OS_ERROR unless $OS_ERROR{EINTR};
}

alarm(0);
$SIG{ALRM} = 'DEFAULT';

system @ARGV;
exit($CHILD_ERROR >> 8);

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2006 MetaBrainz Foundation

This file is part of MusicBrainz, the open internet music database,
and is licensed under the GPL version 2, or (at your option) any
later version: http://www.gnu.org/licenses/gpl-2.0.txt

=cut
