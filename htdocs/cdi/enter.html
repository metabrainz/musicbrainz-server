<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# Add Release workflow.
	#
	# $Id$
	#
</%perl>
<%args>

	# these first 3 parameters are required.
	$artistid => 0
	$tracks => undef
	$hasmultipletrackartists => undef
    $releasembid => ""

	# cd-toc attach /bare/cdlookup.html
	$discid => ""
	$toc => ""
	$search => ""

	# freedb lookup?
	$freedb => 0

	# copied from /freedb/final.html
	$freedbid => ""
	$freedbcat => ""
	$durations => ""

</%args>
<%perl>

	# only logged in users with a valid e-mail address can
	# see this page.
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	# check if we have a valid number of tracks, else
	# redirect to the add release page.
	unless (MusicBrainz::Server::Validation::IsNonNegInteger($tracks) and $tracks)
	{
		$m->comp("/comp/redirect", '/edit/album/add.html?artistid=' . $artistid . '&error=1');
		return;
	}

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# make sure we got a valid tracks parameter
	MusicBrainz::Server::Validation::IsNonNegInteger($tracks) && $tracks
		or return $m->comp("/comp/layout/badarguments",
			text => "The argument <strong>tracks</strong> is missing or has a wrong format.");

	# make sure we got a valid hasmultipletrackartists parameter
	$hasmultipletrackartists =~ /^0|1$/
		or return $m->comp("/comp/layout/badarguments",
			text => "The argument <strong>hasmultipletrackartists</strong> is missing or has a wrong format.");

	# load musicbrainz object, and make it available in the ARGS hash.
	my $mb = $m->comp("/comp/dblogin");
	$ARGS{"mb"} = $mb;

	if (MusicBrainz::Server::Validation::IsGUID($artistid))
	{
		my $obj = $m->comp("/comp/loadartist", $mb, $artistid);
		$artistid = $obj->GetId;
	}

	# make sure we got a valid artistid parameter
	MusicBrainz::Server::Validation::IsNonNegInteger($artistid) 
		or return $m->comp("/comp/layout/badarguments",
			text => "The argument <strong>artistid</strong> is missing or has a wrong format.");

	if (!UserStuff->IsMBIDSubmitter($session{"privs"}) || !MusicBrainz::Server::Validation::IsGUID($releasembid))
	{
		$releasembid = "";
		$ARGS{releasembid} = "";
	}

	# SETUP FORM STATE
	# ---------------------------------------------------------------
	# initialise the submit buttons, and un-muddle the "submitvalue"
	# argument if there are more than one.
	%ARGS = $m->comp("/comp/release_editor/init-formstate", %ARGS);
	$ARGS{"v::action"} = "/cdi/enter.html";
	$ARGS{"v::edit_track_data"} = !($toc ne "" or $discid ne "");
	$ARGS{"v::isva"} = ($ARGS{"artistid"} == &ModDefs::VARTIST_ID ? 1 : 0);

	# PREPARE ADD RELEASE FORM VALUES
	# ---------------------------------------------------------------
	%ARGS = $m->comp("/comp/release_editor/init-addrelease", %ARGS);
	$m->comp("/comp/error", $ARGS{"init-addrelease-error"}, 1, 1)
		if ($ARGS{"init-addrelease-error"});

	# HANDLE SPECIAL SUBMIT BUTTONS
	# ---------------------------------------------------------------
	%ARGS = $m->comp("/comp/release_editor/init-handleredirect", %ARGS);

	# HANDLE FREEDB LOOKUP
	# ---------------------------------------------------------------
	%ARGS = $m->comp("/comp/release_editor/init-freedblookup", %ARGS);

	# setup field checks.
	$ARGS{"v::checkedmacsac"} = 1;
	$ARGS{"v::checkedartists"} = !$ARGS{"hasmultipletrackartists"};
	$ARGS{"v::checkedlabels"} = 0;

	my $title = "Add release".($ARGS{"hasmultipletrackartists"} ? " (Various artists)" : "");
	$m->comp("/comp/sidebar-notitle", pagetitle => $title);

	# check if we got a form submit to handle.
	if ($ARGS{"v::formsubmitted"})
	{
		# we always need to check the releases, because else we don't know how
		# many releases we need to display
		%ARGS = $m->comp("/comp/release_editor/check-releases", %ARGS);

		# check if the user clicked on Enable/Disable track artists button
		%ARGS = $m->comp("/comp/release_editor/convert-toggle-trackartists", %ARGS);

		# MOVE RELEASE?
		# ---------------------------------------------------------------
		# if the user has checked the release artist checkbox, present
		# the interface for the "move release" operation.
		if ($ARGS{"v::showform"}
			and $ARGS{"v::validate"}
			and $ARGS{"artistedit"})
		{
			%ARGS = $m->comp("/comp/release_editor/convert-moverelease", %ARGS);
		}

		# CONVERT TO VARIOUS ARTISTS?
		# ---------------------------------------------------------------
		if ($ARGS{"v::showform"}
			and $ARGS{"v::validate"}
			and $ARGS{"submitvalue"} eq $ARGS{"SUBMIT_TOMAC"})
		{
			%ARGS = $m->comp("/comp/release_editor/convert-tomac", %ARGS);
		}

		# CONVERT TO SINGLE ARTIST?
		# ---------------------------------------------------------------
		if ($ARGS{"v::showform"}
			and $ARGS{"v::validate"})
		{
			if (($ARGS{"artistid"} == 0 or defined $ARGS{"sacartist"}) or
				($ARGS{"submitvalue"} eq $ARGS{"SUBMIT_TOSAC"}) or
				($ARGS{"submitvalue"} eq $ARGS{"SUBMIT_SACARTIST_SEARCH"}))
			{
				my $prev = $ARGS{"artistid"};
				%ARGS = $m->comp("/comp/release_editor/convert-tosac", %ARGS);
				if ($ARGS{"artistid"} != $prev)
				{
					$ARGS{"v::isva"} = ($ARGS{"artistid"} == &ModDefs::VARTIST_ID ? 1 : 0);

					my $title = "Add Release".($ARGS{"v::isva"} ? " (Various Artists)" : "");
					$m->comp("/comp/sidebar-notitle", pagetitle => $title);
				}
			}
		}

		# if the above operations did not unset the validation
		# flag, do the validation.
		if ($ARGS{"v::validate"})
		{
			# CHECK OMITTED FIELDS
			# --------------------------------------------------------------
			%ARGS = $m->comp("/comp/release_editor/check-fields", %ARGS);

			# CHECK RELEASE LANGUAGE
			# --------------------------------------------------------------
			%ARGS = $m->comp("/comp/release_editor/check-language-and-script", %ARGS);

			# CHECK RELEASE ATTRIBUTES
			# --------------------------------------------------------------
			%ARGS = $m->comp("/comp/release_editor/check-attributes", %ARGS);

			# SIMILAR RELEASES CHECK
			# ---------------------------------------------------------------
			# check if we have an release with the same number
			# of tracks and a similar title in the database
			$ARGS{"v::checkedsimilar"} = 1;
			%ARGS = $m->comp("/comp/release_editor/check-similar-releases", %ARGS)
				if (!defined $ARGS{"force_similar"});

			# CHECK TRACK ARTISTS
			# --------------------------------------------------------------
			if ($ARGS{"v::showform"} and
				not keys %{$ARGS{"v::fields"}} and
				$ARGS{"hasmultipletrackartists"} and
				(($ARGS{"submitvalue"} eq $ARGS{"SUBMIT_NEXT"}) or
				 ($ARGS{"submitvalue"} eq $ARGS{"SUBMIT_ADDARTIST_DONE"}) or
				 ($ARGS{"submitvalue"} eq $ARGS{"SUBMIT_TRACKARTIST_SEARCH"}) or
				 ($ARGS{"submitvalue"} eq $ARGS{"SUBMIT_TRACKARTIST_SELECT"})))
			{
				%ARGS = $m->comp("/comp/release_editor/check-artists", %ARGS);
			}

			if (not keys %{$ARGS{"v::fields"}} and $ARGS{"v::showform"} and
				(($ARGS{"submitvalue"} eq $ARGS{"SUBMIT_NEXT"}) or
				 ($ARGS{"submitvalue"} eq $ARGS{"SUBMIT_ADDLABEL_DONE"}) or
				 ($ARGS{"submitvalue"} eq $ARGS{"SUBMIT_LABEL_SEARCH"}) or
				 ($ARGS{"submitvalue"} eq $ARGS{"SUBMIT_LABEL_SELECT"})))
			{
				%ARGS = $m->comp("/comp/release_editor/check-labels", %ARGS);
			}

			# CHECK CHARACTERS IN FIELDS WHICH MIGHT MAC/SAC
			# ---------------------------------------------------------------
			# checks if the user tries to enter a multiple artists as
			# a single artist release, and vice-versa.
			# this check is ignored if the force_macsac flag has been set.
			%ARGS = $m->comp("/comp/release_editor/check-macsac", %ARGS);

			# show page depending on editor state.
			# -- v::fields == 0 => all field not empty
			# -- v::attributes == 0 => all attributes not empty
			# -- v::releases == 1 => no invalid date/country found.
			if ($ARGS{"v::showform"} and
				keys %{$ARGS{"v::fields"}} == 0 and
				keys %{$ARGS{"v::attributes"}} == 0 and
				keys %{$ARGS{"v::language"}} == 0 and
				keys %{$ARGS{"v::releases"}} == 1)
			{

				if ($ARGS{"submitvalue"} eq $ARGS{"SUBMIT_NEXT"} and
					$ARGS{"v::checkedmacsac"} &&
					$ARGS{"v::checkedsimilar"} &&
					$ARGS{"v::checkedartists"} &&
					$ARGS{"v::checkedlabels"})
				{
					$m->comp("/comp/release_editor/review", %ARGS);
					$ARGS{"v::showform"} = 0;
				}

				if ($ARGS{"submitvalue"} eq $ARGS{"SUBMIT_ADDRELEASE"})
				{
					$m->comp("/comp/release_editor/entermoderations-add", %ARGS);
					$ARGS{"v::showform"} = 0;
				}
			}
		}
	}

	# if the validation process determined that the user
	# has not filled all the fields yet.
	if ($ARGS{"v::showform"})
	{
		if ($artistid > 0 && !$ARGS{"artistname"})
		{
		   my $ar = MusicBrainz::Server::Artist->new($mb->{DBH});
		   $ar->SetId($artistid);
		   $ar->LoadFromId();
		   $ARGS{"artistname"} = $ar->GetName();
		   $mb->Logout;
		}

		$m->comp("/comp/stylemenu");

		$m->comp("/comp/release_editor/form", %ARGS);
	}

	$m->comp("/comp/release_editor/dump-arguments", %ARGS);

	$m->comp("/comp/footer");

</%perl>

%# vi: set ts=4 sw=4 ft=mason :
