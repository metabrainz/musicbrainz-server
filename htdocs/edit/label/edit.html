<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# Edit an existing label
	#
	# $Id$
	#
</%perl>
<%args>

	$id => ""
	$labelid => undef

	$name => ""
	$sortname => ""
	$labelcode => ""
	$type => -1
	$country => undef

	$begindate_y => ""
	$begindate_m => ""
	$begindate_d => ""
	$enddate_y => ""
	$enddate_m => ""
	$enddate_d => ""

	$resolution => ""
	$confirm => 0

	$submitvalue => ""
	$notetext => ""

</%args>
<%perl>

	# backwards compability.
	$labelid ||= $id;

	# only logged in users can see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	MusicBrainz::Server::Validation::IsNonNegInteger($labelid) && $labelid
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument labelid has an invalid format.");

	$labelid != &ModDefs::DLABEL_ID
		or return $m->comp("/comp/layout/badarguments",
			text => "Editing the DELETED_LABEL is not allowed.");

	MusicBrainz::Server::Validation::IsSingleLineString($name)
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument name has an invalid format.");

	MusicBrainz::Server::Validation::IsSingleLineString($sortname)
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument sort name has an invalid format.");

	MusicBrainz::Server::Validation::IsSingleLineString($labelcode)
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument label code has an invalid format.");
	$labelcode =~ s/^LC\s+//;

	MusicBrainz::Server::Validation::IsSingleLineString($resolution)
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument resolution has an invalid format.");

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# Instantiate MusicBrainz object, load label from database
	my $mb = $m->comp("/comp/dblogin");
	my $label = $m->comp("/comp/loadlabel", $mb, $labelid);

	$name = $label->GetName() if !$name;
	$sortname = $label->GetSortName() if !$sortname;

	# Try to load the named label by name. If an label comes up,
	# give the user a stern warning about editing a duplicate label.
	my ($samecheck, $samename) = (1, 0);
	my @dupes;
	my $dupar = MusicBrainz::Server::Label->new($mb->{DBH});
	my $labels = $dupar->GetLabelsFromName($name);
	if (defined $labels && scalar(@$labels))
	{
		foreach my $item (@$labels)
		{
			# do not check same entity.
			next if ($item->GetId == $label->GetId);

			# if entity has the same name, and the resolution
			# is set, store this label as the duplicate.
			$samename = $item->GetName eq $name;
			push @dupes, $item if ($samename);
			if ($samecheck)
			{
				$samecheck = ($samename && $resolution ne "") ? 1 : 0 if ($samename);
				$dupar = $item if ($samename);
			}
		}
	}

	# if we have a value in submitvalue, the form was submitted
	my @messages = ();
	if ($submitvalue ne "")
	{
		my $url = "/label/".$label->GetMBId.".html";

		if ($submitvalue =~ m/Cancel/i)
		{
			return $m->comp("/comp/redirect", $url);
		}
		else
		{

			MusicBrainz::Server::Validation::TrimInPlace($resolution, $name, $sortname);

			push @messages, "Please Select a valid type." if (not MusicBrainz::Server::Label::IsValidType($type));
			push @messages, "Please Enter the Name." if ($name eq "");
			push @messages, "Please Enter the Sort Name." if ($sortname eq "");
			push @messages, "Label Code has a to be a number with max. 5 digits"
				if ($labelcode && not MusicBrainz::Server::Validation::IsValidLabelCode($labelcode));
			push @messages, "Begin Date has to be either valid or empty"
				if (not MusicBrainz::Server::Validation::IsValidDateOrEmpty($begindate_y, $begindate_m, $begindate_d));
			push @messages, "Begin Date has to be either valid or empty"
				if (not MusicBrainz::Server::Validation::IsValidDateOrEmpty($enddate_y, $enddate_m, $enddate_d));
			push @messages, "Begin Date has to be earlier than End Date"
				if (not MusicBrainz::Server::Validation::IsDateEarlierThan($begindate_y, $begindate_m, $begindate_d,
													   $enddate_y, $enddate_m, $enddate_d));
			push @messages, "Please enter a comment and activate the confirm checkbox"
				if (@dupes and ($resolution eq "" or not $confirm));

			push @messages, "You have not made any changes"
				if ($name eq $label->GetName and
					$labelcode eq $label->GetLabelCode and
					$sortname eq $label->GetSortName and
					$resolution eq $label->GetResolution and
					$type == $label->GetType and
					$country == $label->GetCountry and
					MusicBrainz::Server::Validation::MakeDBDateStr($begindate_y, $begindate_m, $begindate_d) eq $label->GetBeginDate and
					MusicBrainz::Server::Validation::MakeDBDateStr($enddate_y, $enddate_m, $enddate_d) eq $label->GetEndDate);

			# no error message was raised, we can go ahead and add
			# the new label entry.
			if (@messages == 0)
			{
				# If the label is not deemed to be the same as another label, don't save the comment
				# --> commented out for now.
				# $resolution = "" if (!$samename);

				$m->comp(
					"/comp/entermods",
					DBH => $mb->{DBH},
					sub => sub {
						my @mods = Moderation->InsertModeration(
							DBH	=> $mb->{DBH},
							uid	=> $session{uid},
							privs => $session{privs},
							type => &ModDefs::MOD_EDIT_LABEL,
							# --
							label => $label,
							name => $name,
							labelcode => $labelcode || "",
							labeltype => $type,
							sortname => $sortname,
							country => $country,
							resolution => $resolution,
							begindate => [ $begindate_y, $begindate_m, $begindate_d ],
							enddate => [ $enddate_y, $enddate_m, $enddate_d ],
						);

						$mods[0]->InsertNote($session{'uid'}, $notetext)
							if $mods[0] and $notetext =~ /\S/;
					},
					success_url => $url,
				) or return;

				die "Shouldn't get here";
			}
		}
	}
	else
	{
		# Set reasonable default values
		$name = $label->GetName() if ($name eq "");
		$sortname = $label->GetSortName() if ($sortname eq "");
		$labelcode = $label->GetLabelCode() if ($labelcode eq "");
		$country = $label->GetCountry() if ($country == undef);
		$type = $label->GetType() if ($label->GetType() and $type == -1); # else we default to 0
		$resolution = $label->GetResolution() if ($resolution eq "");
		($begindate_y, $begindate_m, $begindate_d) = $label->GetBeginDateYMD()
			unless grep { $_ } ($begindate_y, $begindate_m, $begindate_d);
		($enddate_y, $enddate_m, $enddate_d) = $label->GetEndDateYMD()
			unless grep { $_ } ($enddate_y, $enddate_m, $enddate_d);
	}

</%perl>

<& /comp/sidebar-notitle, pagetitle => "Edit Label" &>

	<script type="text/javascript" src="/scripts/labelform.js"></script>

	<form method="post" action="/edit/label/edit.html">

		<& /comp/autofixmenu &>

		<& /comp/layout/editformbegin, title => "Edit Label", label => $label &>

		<table class="formstyle">
			<tr>
				<td colspan="2" class="instructions">
					<ul>
						<li>
							Please read the <& /comp/linkdoc, "StyleGuideline", "Style Guidelines" &>,
							then use the fields below to update the information of
							this label.</li>
						<li>
							If one or more labels with the same name already exist in the
							database, you will be prompted to add a comment to this label
							that will allow users to distinguish this entry from the other
							label(s) with the same name.
							(see <& /comp/linkdoc, "LabelComment", "Label Comment" &>)</li>
						<li>
							Please note that casing of the label is taken into account when
							determining the need for a comment; The two Labels <em>somename</em>
							and <em>Somename</em> are not the same.</li>
					</ul>
				</td>
			</tr>
		</table>

		<table class="formstyle">

			<& /edit/label/main-form,
				action			=> '/edit/label/edit.html',
				label			=> $label,
				name			=> $name,
				labelcode		=> $labelcode,
				sortname		=> $sortname,
				country			=> $country,
				type			=> $type,
				resolution		=> $resolution,
				begindate		=> [ $begindate_y, $begindate_m, $begindate_d ],
				enddate			=> [ $enddate_y, $enddate_m, $enddate_d ],
				notetext		=> $notetext,

				dupes			=> \@dupes,
				messages		=> \@messages,
				confirm			=> $confirm
			&>

		</table>
		<& /comp/layout/editformend &>

	</form>

<& /comp/footer &>

%# vi: set ts=4 sw=4 ft=mason :
