<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# Add a new label
	#
	# $Id$
	#
</%perl>
<%args>

	$initial_name => ""
	$returnurl => ""

	$name => ""
	$labelcode => ""
	$sortname => ""
	$country => undef
	$type => 0
	$begindate_y => ""
	$begindate_m => ""
	$begindate_d => ""
	$enddate_y => ""
	$enddate_m => ""
	$enddate_d => ""

	$resolution => ""
	$confirm => 0

	$submitvalue => ""
	$notetext => ""

</%args>
<%perl>

	# only logged in users can see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	MusicBrainz::Server::Validation::IsSingleLineString($name)
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument name has an invalid format.");

	MusicBrainz::Server::Validation::IsSingleLineString($sortname)
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument sort name has an invalid format.");

	MusicBrainz::Server::Validation::IsSingleLineString($labelcode)
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument label code has an invalid format.");

	MusicBrainz::Server::Validation::IsSingleLineString($resolution)
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument resolution has an invalid format.");

	MusicBrainz::Server::Validation::IsNonNegInteger($type) && MusicBrainz::Server::Label::IsValidType($type)
		or return $m->comp("/comp/layout/badarguments",
			text =>"Argument type is invalid.");

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# Instantiate MusicBrainz object
	my $mb = $m->comp("/comp/dblogin");

	# Try to load the named label by name. If an label comes up, give
	# the user a stern warning about adding a duplicate label.
	my @dupes;
	if ($name ne "")
	{
		my $label = MusicBrainz::Server::Label->new($mb->{DBH});
		my $labels = $label->GetLabelsFromName($name);

		my $norm_name = MusicBrainz::Server::Validation::NormaliseSortText($name);
		@dupes = grep {
			MusicBrainz::Server::Validation::NormaliseSortText($_->GetName) eq $norm_name
		} @$labels;
		
		if ($labelcode && MusicBrainz::Server::Validation::IsValidLabelCode($labelcode)) {
			$labels = $label->GetLabelsFromCode($labelcode);
			push @dupes, @$labels;
		}
	}

	# if we have a value in submitvalue, the form was submitted
	my @messages = ();
	if ($submitvalue ne "")
	{
		if ($submitvalue =~ m/Cancel/i)
		{
			# if a return url was given, return to this page
			if ($returnurl ne "")
			{
				$returnurl .= "&amp;submitvalue=AddLabelCancelled";

				# /comp/redirect failed, probably because the URL was too long.
				$r->internal_redirect($returnurl);
				$m->auto_send_headers(0);
				$m->clear_buffer;
				$m->abort;
			}
			return $m->comp("/comp/redirect", "/search.html");
		}
		else
		{
			MusicBrainz::Server::Validation::TrimInPlace($resolution, $name, $sortname);

			push @messages, "Please Select a valid type." if (not MusicBrainz::Server::Label::IsValidType($type));
			push @messages, "Please enter the Name." if ($name eq "");
			push @messages, "Please enter the Sort Name." if ($sortname eq "");

			push @messages, "Label Code has a to be a number with max. 5 digits"
				if ($labelcode && not MusicBrainz::Server::Validation::IsValidLabelCode($labelcode));
			push @messages, "Begin Date has to be either valid or empty"
				if (not MusicBrainz::Server::Validation::IsValidDateOrEmpty($begindate_y, $begindate_m, $begindate_d));
			push @messages, "End Date has to be either valid or empty"
				if (not MusicBrainz::Server::Validation::IsValidDateOrEmpty($enddate_y, $enddate_m, $enddate_d));
			push @messages, "Begin Date has to be earlier than End Date"
				if (not MusicBrainz::Server::Validation::IsDateEarlierThan($begindate_y, $begindate_m, $begindate_d,
													   $enddate_y, $enddate_m, $enddate_d));
			push @messages, "Please enter a comment and activate the confirm checkbox"
				if (@dupes and ($resolution eq "" or not $confirm));

			# no error message was raised, we can go ahead and add
			# the new label entry.
			if (@messages == 0)
			{
				# If the label is not deemed to be the same as another label, don't save the comment
				# --> commented out for now.
				# $resolution = "" if not @dupes;

				# enter edit.
				my $ret = $m->comp(
					"/comp/entermods",
					DBH => $mb->{DBH},
					sub => sub {
						my @mods = Moderation->InsertModeration(
							DBH	=> $mb->{DBH},
							uid	=> $session{uid},
							privs => $session{privs},
							type => &ModDefs::MOD_ADD_LABEL,
							# --
							name => $name,
							sortname => $sortname,
							labelcode => $labelcode,
							labeltype => $type,
							label_country => $country || "",
							label_resolution => $resolution,
							label_begindate => [ $begindate_y, $begindate_m, $begindate_d ],
							label_enddate => [ $enddate_y, $enddate_m, $enddate_d ],
						);

						$mods[0]->InsertNote($session{'uid'}, $notetext)
							if $mods[0]
							and $notetext =~ /\S/;

						# Return the mods inserted - see below
						@mods;
					},
				) or return;

				# let's see if that turned out ok.
				(my $addmod) = grep { $_->Type eq &ModDefs::MOD_ADD_LABEL } @$ret;
				if ($addmod)
				{
					my $rowid = $addmod->GetRowId;

					# if a return url was given, append new labelid and
					# return to the other page.
					if ($returnurl ne "")
					{
						# load submit values from releas editor formstate component,
						# and use the Select Label button to submit the newly
						# added label to the editor.
						my $submitvalue;
						if ($returnurl =~ /albumreleases/)
						{
							$submitvalue = "&submit_select=1";
						}
						else
						{
							my %cfg = $m->comp("/comp/release_editor/init-formstate", %ARGS);
							$submitvalue = "&submitvalue=" . uri_escape($cfg{"SUBMIT_ADDLABEL_DONE"});
						}

						# append new rowid, and submitvalue to the given returnurl.
						$returnurl = $returnurl . $rowid . $submitvalue;

						# /comp/redirect failed, probably because the URL was too long.
						$r->internal_redirect($returnurl);
						$m->auto_send_headers(0);
						$m->clear_buffer;
						$m->abort;
					}

					# else show label page of newly created label.
					return $m->comp("/comp/redirect", "/show/label/?labelid=" . $rowid);
				}
				return $m->comp("/comp/redirect", "/search.html");

				die "Shouldn't get here";
			}
		}
	}

</%perl>

<& /comp/sidebar-notitle, pagetitle => "Add New Label" &>

	<script type="text/javascript" src="/scripts/labelform.js"></script>

	<form method="post" action="/edit/label/add.html">

		<& /comp/autofixmenu &>

		<& /comp/stylemenu &>

		<& /comp/layout/editformbegin, title => "Add New Label" &>

		<table class="formstyle">
			<tr>
				<td colspan="2" class="instructions">
					<ul>
						<li>
							You are about to add an label to MusicBrainz.</li>
						<li>
							Please read the <& /comp/linkdoc, "StyleGuideline", "Style Guidelines" &>,
							then provide the necessary information about the new
							label below.</li>
						<li>
							More information about labels can be found on
							<& /comp/linkdoc, "LabelResource", "Label Resources" &>.</li>
						<li>
							If one or more labels with the same name already exist in the
							database, you will be required to add a comment to this label
							that will allow users to distinguish this entry from the other
							label(s) with the same name. (see <& /comp/linkdoc, "LabelComment", "Label Comment" &>)</li>
					</ul>
				</td>
			</tr>
		</table>
		<table class="formstyle">

			<& /edit/label/main-form,
				action		=> '/edit/label/add.html',
				name		=> $name || $initial_name,
				labelcode	=> $labelcode,
				type		=> $type,
				country		=> $country,
				sortname	=> $sortname,
				resolution	=> $resolution,
				begindate	=> [ $begindate_y, $begindate_m, $begindate_d ],
				enddate		=> [ $enddate_y, $enddate_m, $enddate_d ],
				notetext	=> $notetext,

				dupes		=> \@dupes,
				confirm		=> $confirm,

				messages	=> \@messages,
				returnurl	=> $returnurl
			&>
		</table>

		<& /comp/layout/editformend &>
		<& /comp/movefocus, id => "editlabel_name" &>
	</form>

<& /comp/footer &>

%# vi: set ts=4 sw=4 ft=mason :
