<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# This page allows an AutoEditor to approve a pending edit
	#
	# $Id$
	#
</%perl>
<%args>

	$modid => 0
	$editid => undef

	$notetext => ""
	$url => ""
	$submitvalue => ""
        $inline => 0

</%args>
<%perl>

	$editid ||= $modid;

	# only logged in users can see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	# check if we got a bad editid argument
	MusicBrainz::Server::Validation::IsNonNegInteger($editid) && $editid
		or return $m->comp("/comp/layout/badarguments",
			text => "The argument <strong>editid</strong is either missing or invalid.");

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# Instantiate MusicBrainz object, and load the edit
	# from the database.
	my $mb = $m->comp("/comp/dblogin");

	my $edit = Moderation->new($mb->{DBH});
	$edit = $edit->CreateFromId($editid);

	$edit->PostLoad;
	$edit->PreDisplay;

	# Check if the current user has enough permissions
	my $isautoeditor = (UserStuff->IsAutoEditor($session{"privs"}) or
						UserStuff->IsAutoEditor($session{"orig_privs"}))
		or return $m->comp("/comp/layout/error",
			title => "Edit cannot be approved",
			text => qq!You must be a AutoEditor to approve this edit.!);

	# check if the current edittype would be auto-approved for autoeditors.
	$edit->IsAutoEditType($edit->GetType)
		or return $m->comp("/comp/layout/error",
			title => "Edit cannot be approved",
			text => qq!The edit with <strong>Edit ID</strong> #$editid cannot be approved.
		  			   (Reason: not an autoedit even for autoeditors)!);

	# Check if this specific edit would be auto-approved for autoeditors.
	# But autoeditors may apply edits that are not auto-approved for normal users.
	# $edit->IsAutoEdit($isautoeditor)
	# 	or return $m->comp("/comp/layout/error",
	# 		title => "Edit cannot be approved",
	# 		text => qq!The edit with <strong>Edit ID</strong> #$editid cannot be approved.
	# 	  			   (Reason: removal of data is not an autoedit,
	# 	  			    even for autoeditors)!);

	# determine if the edit note is required:
	#
	# * edits with NO-votes require a edit note.
	# * if only the editor attempting to approve the edit has voted NO,
	#   they can still approve it.
	#
	my $vote = $edit->VoteFromUser($session{"uid"});
	my $optionalNote = $edit->GetNoVotes == 0 ||
					  ($edit->GetNoVotes == 1 && defined $vote && $vote->GetVote == &ModDefs::VOTE_NO);

	# check if we got a form submission to handle
	if ($submitvalue ne "")
	{
		# redirect to the show edit detail page if no
		# returnurl is given.
		$url = "/show/edit/?editid=$editid"
			if (!$url);

		# The approve button was clicked.
		if ($submitvalue =~ m/Cancel/i)
		{
			# redirect to calling page.
			return $m->comp("/comp/redirect", $url);
		}
		else
		{
			MusicBrainz::Server::Validation::TrimInPlace($notetext);

			# Approve the edit
			if ($optionalNote or $notetext ne "")
			{
				my $sql = Sql->new($mb->{DBH});

				my $vertmb = new MusicBrainz;
				$vertmb->Login(db => 'RAWDATA');
				my $vertsql = Sql->new($vertmb->{DBH});

				eval {
					$sql->Begin;
					$vertsql->Begin;

					$Moderation::DBConnections{READWRITE} = $sql;
					$Moderation::DBConnections{RAWDATA} = $vertsql;

					# Insert a "yes" vote
					my %votes = ($editid => &ModDefs::VOTE_YES);
					my $v = MusicBrainz::Server::Vote->new($mb->{DBH});
					$v->InsertVotes(\%votes, $session{"uid"}, 1);

					# Approve the edit
					my $status = $edit->ApprovedAction;
					$edit->SetStatus($status);
					$edit->CloseModeration($status);

					# update applied edits count of the user who entered the edit.
					my $user = UserStuff->new($mb->{DBH});
					$user->CreditModerator($edit->GetModerator, $status);

					# Insert a note
					$edit->InsertNote($session{"uid"}, $notetext)
						if ($notetext);

					$sql->Commit;
					$vertsql->Commit;
				}
				or do
				{
					my $err = $@;
					$sql->Rollback;
					$vertsql->Rollback;

					$m->comp("/comp/layout/error",
						title => "Error applying edit:",
						text => "<pre>$err</pre>"
					);
					return undef;
				};

				# redirect to calling page.
				return $m->comp("/comp/redirect", $url);
			}
		}
	}

</%perl>

% if ($inline) {
    <& /comp/header_inline, title => "Approve edit", nosmallfonts => 1, noentityicons => 0 &>
% } else {
    <& /comp/sidebar-notitle, pagetitle => "Approve edit" &>
% }

	<& /comp/tablebegin, title => "Approve edit" &>

	<form method="post" action="/mod/approve.html">

		<table class="formstyle">
			<tr>
				<td colspan="2" class="instructions">
					<ul>
						<li>Please read the <& /comp/linkdoc, "CodeOfConduct", "Code of Conduct" &>
							and make sure you do not violate any of the rules stated therein
							before approving any edits.</li>
						<li>Are you sure you want to approve this edit? </li>
					</ul></td>
			</tr>
		</table>

		<table class="formstyle">


% 	if ($submitvalue ne "" && !$optionalNote && $notetext eq "")
%	{

			<tr class="spaced">
				<td>&nbsp;</td>
				<td>
					<& /comp/form/feedbackbox, "warning", "Edit note required!",
						"You must provide an edit note in order to approve this edit." &></td>
			</tr>

%	}

			<tr class="top">
				<td class="label">Edit:</td>
				<td><%perl>$edit->ShowModType($m, 0);</%perl></td>
			</tr>
			<tr class="spaced top">
				<td class="label">Old value:</td>
				<td><%perl>$edit->ShowPreviousValue($m, 0);</%perl></td>
			</tr>
			<tr class="top">
				<td class="label">New value:</td>
				<td><%perl>$edit->ShowNewValue($m, 0);</%perl></td>
			</tr>

			<tr>
				<td>&nbsp;</td>
				<td>
					<& /comp/notetext,
						required => !$optionalNote,
						buttons => [["Approve edit &raquo;", "btnYes"], ["Cancel", "btnCancel"]],
						showprivscheckbox => 0,
						showeditnotetext => 0
					&>

					<& /comp/form/hiddenfield, "editid", $editid &>
					<& /comp/form/hiddenfield, "url", $url &>
					<& /comp/form/focusfield, "btnYes" &>
				</td>
			</tr>
		</table>

	</form>

	<& /comp/tableend &>

	<& /comp/js/diffcollapse &>

% if ($inline) {
    <& /comp/footer_inline &>
% } else {
    <& /comp/footer &>
% }

%# vi: set ts=4 sw=4 ft=mason :
